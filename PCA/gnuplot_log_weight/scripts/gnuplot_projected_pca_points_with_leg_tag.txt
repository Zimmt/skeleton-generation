# 2D plots of projection of PCA data on first three eigenvectors

set terminal pdf size 11cm, 10cm
set key box opaque
set key above
NO_LEG = 'pointtype 6 pointsize 0.5 linecolor rgb "red"'
ONE_LEG = 'pointtype 10 pointsize 0.5 linecolor rgb "blue"'
TWO_LEGS = 'pointtype 4 pointsize 0.5 linecolor rgb "green"'

set xrange [-2:2]
set yrange [-2:2]
set zrange [-2:2]
set size square

set xlabel "erster Eigenvektor"
set ylabel "zweiter Eigenvektor"
set output "projection_eigenvectors12.pdf"
a = 1
b = 2
plot "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 0.0 ? column(b) : 1/0) title "0 Beine" @NO_LEG, \
     "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 1.0 ? column(b) : 1/0) title "2 Beine" @ONE_LEG, \
     "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 2.0 ? column(b) : 1/0) title "4 Beine" @TWO_LEGS

set ylabel "dritter Eigenvektor"
set output "projection_eigenvectors13.pdf"
a = 1
b = 3
plot "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 0.0 ? column(b) : 1/0) title "0 Beine" @NO_LEG, \
     "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 1.0 ? column(b) : 1/0) title "2 Beine" @ONE_LEG, \
     "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 2.0 ? column(b) : 1/0) title "4 Beine" @TWO_LEGS

set xlabel "zweiter Eigenvektor"
set output "projection_eigenvectors23.pdf"
a = 2
b = 3
plot "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 0.0 ? column(b) : 1/0) title "0 Beine" @NO_LEG, \
     "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 1.0 ? column(b) : 1/0) title "2 Beine" @ONE_LEG, \
     "../input/projected_pcaPoints_with_leg_tag.txt" using a:(column(4) == 2.0 ? column(b) : 1/0) title "4 Beine" @TWO_LEGS
